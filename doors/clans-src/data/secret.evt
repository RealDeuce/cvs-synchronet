# The Youth Hostel

>> Comment Begins

What you must do:

- at the bus stop, get on bus #26
- at hostel, you meet a girl who loves cakes
- go to the kitchen, talk to guy there, says he will not give you a cake
  without a meal ticket
- in the bath, you find one (must take a bath first) -- floating in the water
  --> can't get in water before washing self ;)
- get cake
- go to girl who is in the lounge watching tv
- she thanks you --> the end for now

flags:
        T1 = did you talk to the girl yet?
        T2 = Get the meal ticket yet?
        T3 = Got the cake yet?
        T4 = Give her the cake yet?
        T5 = Got coins?
        T6 = Went to reception desk and got key?
        T7 = Got towel?
        T8 = Washed up FIRST?
        T9 = Got bus pass?
        T10= Got KOR manga yet?

>> Comment Ends

Event 2
Text "%C
Text "|12 Utano Youth Hostel
Text
Text "|07It is raining outside.  You sit on the bus and watch the outside world
Text "pass you by.  Soon you hear the words "Utano Yusu Hosteru" uttered by the
Text "bus.  You pay for the ride and get off with your backpack on.
Text
pause
Jump 2.BusStop
END

    Result 2.BusStop
    Text
    Text "|12The Bus Stop
    Text
    Text "|07You are standing at the bus stop on the left side of the road.  (They DO
    Text "drive on the other side of the road, you know.  You watch the traffic go
    Text "by as the rain pitter patters on the road.  It is pouring.  To the north
    Text "you can see a sign that points up an alley towards the hostel.
    Text
    {T4}Text "The [G]irl is here and so is the [B]us.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N
    Text
    Prompt "`02Where to? `0A
    {T4}Option G 2.BusStop.Girl
    {T4}Option B 2.BusStop.Bus
    Option N 2.Alley
    Option Q Stop
    END

        Result 2.BusStop.Girl
        Text
        Text "|15"Let's go to the shopping arcade." |07she says.
        pause
        Jump 2.BusStop
        End

        Result 2.BusStop.Bus
        {!T9}Text "You're about to get on the bus when you realize you don't have your bus
        {!T9}Text "pass on you!  Realizing you don't want to pay, you decide to get off the
        {!T9}Text "bus and not take it till you find your bus pass.
        {!T9}Jump 2.BusStop
        Text
        Text "|07You hop on the bus and head to the shopping arcade.  It's still raining,
        Text "but the sun shines in this girl's eyes.
        pause
        End

    Result 2.Alley
    Text
    Text "|12The Alley
    Text
    Text "|07You are walking along an alley that leads north and south.  To the
    Text "north you can see the road stretches on for a bit more and then abruptly
    Text "changes direction.  To the west is the entrance to the Utano Youth Hostel.
    Text "To the south you see the alley leads to the bus stop where you got off.
    Text
    Text "There is a [P]hone here.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F S W
    Text
    Prompt "`02Where to? `0A
    Option P 2.Alley.Phone
    Option S 2.BusStop
    Option W 2.Entrance
    Option Q Stop
    END

        Result 2.Alley.Phone
        Text "|07You pick up the phone and call home.  Ahh, home sweet home.  You start
        Text "having thoughts about what it's like to sleep in your own bed and how
        Text "it is already late evening there...  Soon the time runs out on your phone card
        Text "and you realize you're still in a far away land.
        Text
        pause
        Jump 2.Alley
        END

    Result 2.Entrance
    Text
    Text "|12The Entrance
    Text
    Text "|07You are standing at the entrance to the youth hostel.  You are inside.
    Text "To the north you see a small lounge with a TV.  Some hostellers are watching
    Text "it but you can't see what's showing.  To the West you see the
    Text "reception desk.  To the south is the dining area.  To the east is the alley.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N S E W
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option E 2.Alley
    Option W 2.Reception
    Option N 2.Lounge
    Option S 2.Dining.1
    END
    
    Result 2.Reception
    Text
    Text "|12The Reception Desk
    Text
    Text "|07You are at the Reception Desk.  To the south is the laundry room, to the
    Text "east is the entrance and to the north is the lounge.
    {!T6}Text
    {!T6}Text "A guy in his 20s sees you and looks over your papers and eventually hands you
    {!T6}Text "a map and tells you you are in room 18 upstairs.  You thank him.
    {!T6}SetFlag T6
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N S E
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option E 2.Entrance
    Option N 2.Lounge
    Option S 2.Laundry
    END

    Result 2.Laundry
    Text
    Text "|12The Laundry Room
    Text
    Text "|07You're in the laundry room.  To the north is the reception desk.
    Text
    {!T7}Text "You see a [T]owel on the floor.
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    {!T7}Option T 2.Laundry.Towel
    Option N 2.Reception
    END

        Result 2.Laundry.Towel
        Text "
        Text "You pick up the towel and stick the whole thing in your back pocket.
        SetFlag T7
        pause
        Jump 2.Laundry
        END
    
    Result 2.Lounge
    Text
    Text "|12The Lounge
    Text
    Text "|07You see many [P]eople watching [T]V in the lounge.  To the south is the
    Text "entrance to the hostel.  To the west you see a hallway.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F S W
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option P 2.Lounge.People
    Option W 2.1FHall1
    Option S 2.Entrance
    Option T 2.Lounge.TV
    END

        Result 2.Lounge.TV
        Text
        Text "A baseball game is on.  You'd change the channel, but the fat guy watching
        Text "the game is bigger than you (way bigger).
        pause
        Jump 2.Lounge
        Text

        Result 2.Lounge.People
        Text
        Text "You see a fat white guy sitting with a can of Pocari Sweat in his hand.
        Text "There is also a couple Japanese teenagers discussing something and not paying
        Text "attention to the TV.
        Text

        {T1&!T4}Text "The girl is still here and continues to stare out the window.
        {T1&T4}Text "The girl is long gone.  Where did she go again?

        {!T1&!T3}Text "You see a cute Japanese girl sitting down just staring out the window,
        {!T1&!T3}Text "lost in thought.  She speaks (in English):  "I wish I had a cake."
        {!T1&!T3}SetFlag T1

        {T1&T3&!T4}Text
        {T1&T3&!T4}Text "Here's a cake. you say.  She looks up and sees you holding a cake in
        {T1&T3&!T4}Text "your hand.  Thanks a lot!  She quickly takes the cake and makes short
        {T1&T3&!T4}Text "work of it.  Wanna go somewhere? she asks.  You say Ok.  I'll meet
        {T1&T3&!T4}Text "you at the bus stop she says.
        {T1&T3&!T4}SetFlag T4
        pause
        Jump 2.Lounge
        END

    Result 2.1FHall1
    Text
    Text "|12The Downstairs Hallway
    Text
    Text "|07You are in the hallway.  To the west is more hallway and to the north are some
    Text "stairs leading up.  To the east is the lounge.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N W E
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option N 2.Stairs
    Option W 2.1FHall2
    Option E 2.Lounge
    END

    Result 2.Stairs
    {!T6}Text
    {!T6}Text "I'm sure it would be best if you went to the reception desk FIRST!
    {!T6}pause
    {!T6}Jump 2.1FHall1
    Text
    Text "|12The Stairs
    Text
    Text "|07You are standing on some stairs that lead up to the second floor and down
    Text "to the first floor.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F U D
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option D 2.1FHall1
    Option U 2.2FHall1
    END

    Result 2.2FHall1
    Text
    Text "|12The Upstairs Hallway
    Text
    Text "|07You are on the second floor of the hostel.  To the east you can see
    Text "a balcony.  To the north are flights of stairs leading down.  To the west
    Text "is more hallway.  You see a [P]hone here.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N E
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option N 2.Stairs
    Option E 2.Balcony
    Option P 2.2FHall1.Phone
    END

        Result 2.2FHall1.Phone
        Text
        Text "|07You pick up the phone but it doesn't accept long distance calls so you put
        Text "down the receiver.
        pause
        Jump 2.2FHall1
        End

        Result 2.Balcony
        Text "|12The Balcony
        Text
        Text "|07You slip on a pair of balcony slippers and walk onto the balcony.
        Text "It would be enjoyable if it wasn't raining.
        Text
        {!T10}Text "|07You see a copy of [K]OR manga volume #10.
        {!T10}Text
        Text
        Text "`02[`0AQ=Quit`02] `03Directions:`0F W
        Text
        Prompt "`02Where to? `0A
        Option Q Stop
        Option W 2.2FHall1
        {!T10}Option K 2.Balcony.KOR
        END

        Result 2.Balcony.KOR
        Text "|07You pick up the KOR manga (v10).  Even though it's soaking wet, it's
        Text "still readable.  You flip through it a bit before sticking it into
        Text "your back pocket (it's a HUGE pocket).
        SetFlag T10
        pause
        Jump 2.Balcony
        END

    Result 2.1FHall2
    Text
    Text "|12The Downstairs Hallway
    Text
    Text "|07You are in the hallway.  To the west is the female sleeping area.  To the
    Text "north is the men's bath and to the east is some more hallway.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N W E
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option N 2.Bath
    Option W 2.Women
    Option E 2.1FHall1
    END

        Result 2.Women
        Text
        Text "Oh, I should've mentioned it before.  You play a male in this "game" and
        Text "yer not allowed in the female area.  Tough (pervert)!
        pause
        Jump 2.1FHall2
        END

    Result 2.Bath
    ClearFlag T8
    Text
    Text "|12The Changing Room
    Text
    Text "|07You are in the changing room of the bath.  Basically, you strip here and go
    Text "bathe in the bathing area.  Oh yeah, bring a towel.
    Text
    Text "To the south is the hallway, to the east is the bathing area.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F S E
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option S 2.1FHall2
    {T7}Option E 2.Bath.Wash
    {!T7}Option E 2.Bath.Wash.NoTowel
    END

        Result 2.Bath.Wash.NoTowel
        Text
        Text "You should probably get a towel first to cover yourself up a bit.
        pause
        Jump 2.Bath
        END

        Result 2.Bath.Wash
        Text
        Text "|12The Bathing Area
        Text
        Text "|07You're in the bathing area and there are a few other nude men so try not to
        Text "look down.  To the west is the changing room.  You see a giant [H]ot tub and
        Text "individual washing [F]acilities (basically, a small stool, some soap, a mirror,
        Text "a shower-head, and a basin.
        Text
        Text "`02[`0AQ=Quit`02] `03Directions:`0F W
        Text
        Prompt "`02Where to? `0A
        Option Q Stop
        Option W 2.Bath
        Option H 2.Tub
        Option F 2.WashUp
        END

        Result 2.Tub
        Text
        {!T8}Text "You are about to jump into the steamy hot tub when somebody stops you.
        {!T8}Text "It seems it is customary to wash up BEFORE going into the tub.  So wash up!
        {T8}Text "You jump into the tub and relax for a few minutes.  Ahh, it's nice and hot.
        {T8&!T2}Text
        {T8&!T2}Text "You suddenly see a plastic object floating in the tub.  It's a
        {T8&!T2}Text "meal ticket!  You quickly snatch it up and put it in your uh...
        {T8&!T2}Text "pocket... yeah, that's it....
        {T8&!T2}SetFlag T2
        pause
        Jump 2.Bath.Wash
        END

        Result 2.WashUp
        Text
        Text "You sit down on a stool and proceed to wash yourself (of course, you DID
        Text "remove your towel, right?).  After washing up thoroughly, you feel refreshed!
        SetFlag T8
        pause
        Jump 2.Bath.Wash
        END


    Result 2.Dining.1
    Text
    Text "The Dining Area
    Text
    Text "|07You are standing in the dining area for the youth hostel.  You can see
    Text "some [V]ending machines selling beverages of all sorts.  To the east you
    Text "see a self-serve kitchen.  To the south is more of the dining area.  To the
    Text "north is the entrance to the hostel.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N S E
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option N 2.Entrance
    Option S 2.Dining.2
    Option E 2.Kitchen
    Option V 2.Dining.1.Vending
    END

        Result 2.Dining.1.Vending
        Text
        Text "|07You insert a 110 yen into the machine and get yourself a nice cold
        Text "glass of Pocari Sweat.  Ahh, refreshing.
        Text
        Pause
        Jump 2.Dining.1
        END

    Result 2.Kitchen
    Text
    Text "The Kitchen
    Text
    Text "|07The kitchen is fairly small.  There's a fridge, a stove, and a bunch of
    Text "other things you'd normally find in a kitchen.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F W
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option W 2.Dining.1
    END

    Result 2.Dining.2
    Text
    Text "The Dining Area
    Text
    Text "|07You are standing in the southern portion of the dining area.  There is
    Text "a [P]iano here.  To the east is the w[I]ndow leading to the kitchen (not
    Text "the same as the self-serve kitchen).  To the north is more dining area.
    Text "To the west you can see a garden
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F N W
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option N 2.Dining.1
    Option P 2.Dining.2.Piano
    Option I 2.Dining.2.Window
    Option W 2.Garden
    END

        Result 2.Dining.2.Piano
        Text
        Text "The Piano
        Text
        Text "|07You sit down and begin to play the piano.  You play whatever it is on
        Text "your mind.  Soon the fun is over and you get up.
        pause
        Jump 2.Dining.2
        END

        Result 2.Dining.2.Window
        Text
        Text "The Window
        Text
        Text "|07You look inside the window and see a few people busily preparing
        Text "a meal...
        Text
        {T1&!T3&!T2}Text "YOU SEE A CAKE!
        {T1&!T3&!T2}Text
        {T1&!T3&!T2}Text "You reach out to take it but your hand is quickly slapped by a large
        {T1&!T3&!T2}Text "man with hairy arms and many tatoos.  NO TICKET, NO CAKE!

        {T3}Text "No more cakes for you.  You've already had yours.

        {T1&T2&!T3}Text
        {T1&T2&!T3}Text "Can I have a cake, please? you ask.  You give your meal ticket and the large
        {T1&T2&!T3}Text "man with hairy arms and many tatoos gives you a whole cake!
        {T1&T2&!T3}SetFlag T3
        pause
        Jump 2.Dining.2
        END

    Result 2.Garden
    Text
    Text "The Garden
    Text
    Text "|07You are in the garden.  How relaxing.
    Text
    Text "`02[`0AQ=Quit`02] `03Directions:`0F E
    Text
    Prompt "`02Where to? `0A
    Option Q Stop
    Option E 2.Dining.2
    END




# ----------------------------------------------------------------------------

Event 3
Text "It's easy to sit there and say you'd like to have more money.
Text " And I guess that's what I like about it.  It's easy.  Just
Text "sitting there, rocking back and forth, wanting that money.
Text
Text "`03Secret coming soon!
pause
END

Event 4
Text "`07I've got a magic wand to see her in the morning.
Text "Crystal eyes to read between the lines.
Text
pause
END

Event 5
Text "`0A
Text "Whether they live in an igloo or a grass shack or a mud hut,
Text "people around the world all want the same thing: a better
Text "house.
pause
Jump ProjectX
END

# ----------------------------------------------------------------------------
Result ProjectX
    Text "%C
    Text "|08LOADING THE RALM...
    Pause

    display "/px/Title
    pause
    display "/px/Pg
    pause
    Jump PX.Loop
End

Result PX.Loop
Text "%C
Text
Text
Text
Text "                          |15Caveman Technologies Presents
Text
Text "                              |14Project-X v99.80Special
Text "                             |04A Game by |12Al Shunsler
Text "                               |04copyrigt (|06c|04) |121996
Text
Text "                   |05This game has been running for |13several |05days
Text "                 |05Players are deleted after |1314 |05days of inactivity
Text "                 |13"Bringing you yesterday's technology....today!
Text
Text "                            |04UNREGISTERED:  |24NOT WINNABLE! (don't bother!)|16
Text
Text "                           |06(|05E|06) |12Enter this Realm of Mystery
Text "                           |06(|05L|06) |12List Active Players
Text "                           |06(|05N|06) |12News
Text "                           |06(|05R|06) |12Registration Info
Text "                           |06(|05Q|06) |12Quit Game
Text
Prompt "                             |06Enter Choice Now : |12
Option S PX.Stupid
Option N PX.News
Option E PX.Enter
Option Q PX.Quit
Option L PX.List
Option ~ PX.Secrets
Option R PX.RegInfo

Result PX.Stupid
  Text
  Text "|03You pressed |11S|03.  S is for STUPID!
  Text
  pause
  Text
  Text "|12New Year's Day:  |04A Story
  pause
  Text
  Text "|06Once upon a time, there was a kid named Johnny.
  pause
  Text
  Text "|12The End.
  pause
  Jump PX.Loop
End

Result PX.News
  Text
  Text "|03If you want news, go watch |11CNN|03!
  pause
  Text
  Text "Ok, fine, here's the news:
  pause
  Display "/px/News
  Jump PX.Loop
End

Result PX.Enter
  Text "This is only a small sampling of Project-X.  To play the whole thing,
  Text "download TXDS and then Project-X from the author's BBS.  (It is a door game
  Text "parody for those of you who are slow and didn't catch on.)
  pause
  Text "%C
End

Result PX.Quit
  Prompt "|04Are you really sure you wanna quit this wicked game? [No] : |04
  Option N PX.NoQuit
  Option ~ PX.NoQuit
  Option Y NextLine

  Prompt "|04You really, really want to quit?! [No] : |04
  Option N PX.NoQuit
  Option ~ PX.NoQuit
  Option Y NextLine

  Prompt "|04You're not serious, are you? [No] : |04
  Option N PX.NoQuit
  Option ~ PX.NoQuit
  Option Y NextLine

  Prompt "|12Ok, for the last time, do you want to quit or not!? [No] : |04
  Option N PX.NoQuit
  Option ~ PX.NoQuit
  Option Y NextLine

  Text "|14Sorry, I just can't let you do that.
  pause
  Jump PX.Loop
End

Result PX.List
  display "/px/LIST
  pause
  Jump PX.Loop
End

Result PX.Secrets
  Text
  Text "You have pressed an invalid key.  Please press one that is valid.  A valid
  Text "key is one of the keys in parentheses (( or )).  After pressing one of those
  Text "keys down, the option will be activated.  When the option is activated, please
  Text "release your finger from the key or else a problem may arise.  Thank you for
  Text "taking the time to read this message.
  pause
  Jump PX.Loop
End

Result PX.NoQuit
  Jump PX.Loop
End

Result PX.RegInfo
 Text "|15REGISTRATION INFO for PROJECT-X!!
 Text
 Text "|03Thank you for reading this section.  Registration is only $20.
 Text "This is easy money to get.  If you need $20 right now, you could try
 Text "this:  Go out on the street with an old hat.  Grab a harmonica.
 Text "Sit down on the sidewalk.  Place the hat right in front of you.  Play
 Text "the harp, er, harmonica and wait.  I'm sure you'll soon rack up the
 Text "cash!  |08(Note:  Author does not suggest actually doing this.)
 Text
 Text "|09Once you get the money together, send the money to the author of
 Text "TXDS and tell him it's a registration for TXDS!  He'll appreciate
 Text "it.
 Text

 Text "|12Special deal (for 1996-1998 only!)
 Text
 Text "|13We at Caveman Technologies are offering this special deal.  If you
 Text "and another sysop register together (and included the reg forms in the
 Text "same envelope) the registration fee will only be $20 EACH!  If a third
 Text "sysop gets in on the deal, add on a measly $20 and you've got yourself
 Text "a deal!  You get a savings of at least 32 cents for postage!!!  I urge
 Text "you to get in on this deal before it's over!!!
 pause
 Jump PX.Loop
End
