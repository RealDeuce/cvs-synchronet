; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Synchronet BBS Software"
#define MyAppVersion "3.16"
#define MyAppPublisher "Rob Swindell"
#define MyAppURL "http://www.synchro.net/"
#define MyAppSupportURL "http://wiki.synchro.net/howto:support"
#define MyAppDownloadURL "http://www.synchro.net/download.html"
#define MyAppExeName "sbbsctrl.exe"
#define sbbsCtrlPanel "Synchronet Control Panel"
#define release "debug"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2AC457EF-EDA4-4BDC-B6C0-D49ADB44ABF5}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} v{#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppSupportURL}
AppUpdatesURL={#MyAppDownloadURL}
DefaultDirName=\sbbs
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
AllowNoIcons=yes
OutputDir=.
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
MinVersion = 5.1
InfoBeforeFile=\install\install.rtf
ChangesEnvironment=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: "startupicon"; Description: "Create a shortcut in the Startup folder"; GroupDescription: "{cm:AdditionalIcons}";

[Files]
Source: "\src\sbbs3\ctrl\sbbsctrl.exe";                         DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\src\sbbs3\chat\chat.exe";                             DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\src\sbbs3\useredit\useredit.exe";                     DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\src\sbbs3\msvc.win32.exe.{#release}\*.exe";           DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\src\sbbs3\msvc.win32.dll.{#release}\*.dll";           DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\src\sbbs3\msvc.win32.dll.release\sbbsexec.dll";       DestDir: "{sys}";
Source: "\src\sbbs3\scfg\msvc.win32.exe.{#release}\scfg.exe";   DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\src\sbbs3\scfg\msvc.win32.exe.{#release}\scfghelp.*"; DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\3rdp\win32.release\mozjs\bin\mozjs185-1.0.dll";       DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\3rdp\win32.release\cryptlib\bin\cl32.dll";            DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\src\sexpots\Release\sexpots.exe";                     DestDir: "{app}\exec";  Flags: ignoreversion
Source: "s:\sbbs\exec\user.com";                                DestDir: "{app}\exec";  Flags: ignoreversion
Source: "s:\sbbs\exec\mlabels.exe";                             DestDir: "{app}\exec";  Flags: ignoreversion
Source: "s:\sbbs\exec\svdmansi.com";                            DestDir: "{app}\exec";  Flags: ignoreversion
Source: "s:\sbbs\exec\dosxtrn.exe";                             DestDir: "{app}\exec";  Flags: ignoreversion
Source: "\stock\node1\node.cnf";                                DestDir: "{app}\node1"; Flags: ignoreversion
Source: "\stock\node1\node.cnf";                                DestDir: "{app}\node2"; Flags: ignoreversion
Source: "\stock\node1\node.cnf";                                DestDir: "{app}\node3"; Flags: ignoreversion
Source: "\stock\node1\node.cnf";                                DestDir: "{app}\node4"; Flags: ignoreversion
Source: "\stock\exec\*";  DestDir: "{app}\exec";  Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "CVS"
Source: "\stock\ctrl\*";  DestDir: "{app}\ctrl";  Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "CVS,*.?.cnf"
Source: "\stock\text\*";  DestDir: "{app}\text";  Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "CVS"
Source: "\stock\xtrn\*";  DestDir: "{app}\xtrn";  Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "CVS,server.ini"
Source: "\stock\docs\*";  DestDir: "{app}\docs";  Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "CVS"
Source: "\stock\web\*";   DestDir: "{app}\web";   Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "CVS"
Source: "s:\sbbs\xtrn\sbl\*.exe"; DestDir: "{app}\xtrn\sbl"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#sbbsCtrlPanel}"; Filename: "{app}\exec\{#MyAppExeName}"
Name: "{group}\Synchronet Configuration"; Filename: "{app}\exec\scfg.exe"
Name: "{group}\Synchronet User Editor"; Filename: "{app}\exec\useredit.exe"
Name: "{group}\Synchronet Initialization File"; Filename: "{app}\ctrl\sbbs.ini"
Name: "{group}\Synchronet Documentation"; Filename: "{app}\docs"
Name: "{group}\Synchronet Web Site"; Filename: "{#MyAppURL}"
Name: "{group}\Synchronet Support"; Filename: "{#MyAppSupportURL}"
Name: "{commonstartup}\{#sbbsCtrlPanel}"; Filename: "{app}\exec\{#MyAppExeName}"; Tasks: startupicon
Name: "{commondesktop}\{#sbbsCtrlPanel}"; Filename: "{app}\exec\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\exec\{#MyAppExeName}"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\exec\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Parameters: "{app}\ctrl"; Flags: nowait postinstall skipifsilent

[Registry]
Root: HKLM; Subkey: "System\CurrentControlSet\Control\Session Manager\Environment"; ValueType: string; ValueName: "SBBSCTRL"; ValueData: "{app}\ctrl"

[INI]
Filename: "{app}\ctrl\sbbs.ini"; Section: "Global"; Key: "CtrlDirectory"; String: "{app}\ctrl"
